buildscript {
    repositories {
        jcenter()
    }
}

plugins {
    id "com.jfrog.bintray" version "1.6"
}

ext{
    publishVersion = '2.0-alpha-1'
}

group 'com.github.yoojia'
version publishVersion

apply plugin: 'java'
apply plugin: 'maven'
apply plugin: 'maven-publish'

sourceCompatibility = JavaVersion.VERSION_1_7
targetCompatibility = JavaVersion.VERSION_1_7

repositories {
    jcenter()
    mavenCentral()
}

publishing {
    publications {
        MavenJava(MavenPublication) {
            from components.java
            groupId 'com.github.yoojia'
            artifactId 'next-events'
            version publishVersion
        }
    }
}

// custom tasks for creating source/javadoc jars
task sourcesJar(type: Jar, dependsOn: classes) {
    classifier = 'sources'
    from sourceSets.main.allSource
}

task javadocJar(type: Jar, dependsOn: javadoc) {
    classifier = 'javadoc'
    from javadoc.destinationDir
}

javadoc {
    options{
        encoding "UTF-8"
        charSet 'UTF-8'
        author true
        version true
        links "http://docs.oracle.com/javase/7/docs/api"
        title project.name
    }
}

// add javadoc/source jar tasks as artifacts
artifacts {
    archives sourcesJar, javadocJar
}

bintray {
    user = project.hasProperty('bintrayUser') ? project.property('bintrayUser') : System.getenv('BINTRAY_USER')
    key = project.hasProperty('bintrayApiKey') ? project.property('bintrayApiKey') : System.getenv('BINTRAY_API_KEY')
    publications = ['MavenJava']
    pkg {
        repo = 'maven'
        name = 'NextEvents'
        userOrg = 'yoojia'
        licenses = ['Apache-2.0']
        vcsUrl = 'https://github.com/yoojia/NextEvents.git'
        labels = ['Java', 'Event Bus']
        publicDownloadNumbers = true
        version {
            name = publishVersion
            desc = 'NextEvent library '.concat(publishVersion)
            vcsTag = publishVersion
            attributes = ['gradle-plugin': 'com.use.less:com.use.less.gradle:gradle-useless-plugin']
        }
    }
}

dependencies {
    testCompile 'com.google.guava:guava:19.0'
    testCompile group: 'junit', name: 'junit', version: '4.11'
}
